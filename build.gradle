
plugins {
    id 'java'
    id 'jacoco'
    id 'application'
}

group = 'com.giuseppelamalfa'

mainClassName = 'com.giuseppelamalfa.gameofliferemastered.ApplicationFrame'
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories
{
    mavenCentral()
}

compileJava {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

compileTestJava {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}


ext.headlessExcludes = [
    'com/giuseppelamalfa/gameofliferemastered/ui/**',
    'com/giuseppelamalfa/gameofliferemastered/simulation/SimulationRemoteClient**',
    'com/giuseppelamalfa/gameofliferemastered/simulation/SimulationGUIServer**',
    'Tiles/**',
    'tiles.json',
    //'com/giuseppelamalfa/gameofliferemastered/test/**',
    //'test/**'
]

ext.frontendExcludes = [
    'com/giuseppelamalfa/gameofliferemastered/gamelogic/simulation/SimulationCLIServer**',
    'com/giuseppelamalfa/gameofliferemastered/test/**',
    'test/**'
]

tasks.withType(JavaCompile) {
    println "Compiler args: " + options.compilerArgs
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task headless(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.giuseppelamalfa.gameofliferemastered.simulation.SimulationCLIServer'
    }
    archiveBaseName = "liferemastered_server"
    exclude project.ext.headlessExcludes
    with jar
}

jar {
    manifest {
        attributes 'Main-Class': 'com.giuseppelamalfa.gameofliferemastered.ApplicationFrame'
    }
    //exclude project.ext.frontendExcludes
    finalizedBy headless
}

dependencies {
    testImplementation     'junit:junit:4.13'
    
    // https://mvnrepository.com/artifact/org.json/json
    implementation         'org.json:json:20190722' 
}