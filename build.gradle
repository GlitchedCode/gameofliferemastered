
plugins {
    id 'java-library'
    id 'jacoco'
    id 'application'
}

group = 'com.giuseppelamalfa'

mainClassName = 'com.giuseppelamalfa.gameofliferemastered.ApplicationFrame'
sourceCompatibility = "1.8"
targetCompatibility = "1.8"


repositories
{
    jcenter()
}

compileJava {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

compileTestJava {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}


tasks.withType(JavaCompile) {
    println "Compiler args: " + options.compilerArgs
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task frontend(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.giuseppelamalfa.gameofliferemastered.ApplicationFrame'
    }
    archiveBaseName = "gameofliferemastered"
    with jar
}

task headless(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.giuseppelamalfa.gameofliferemastered.gamelogic.simulation.SimulationCLIServer'
    }
    archiveBaseName = "liferemastered_server"
    exclude('com/giuseppelamalfa/gameofliferemastered/ui/**')   
    exclude('com/giuseppelamalfa/gameofliferemastered/gamelogic/simulation/SimulationRemoteClient**')   
    exclude('com/giuseppelamalfa/gameofliferemastered/gamelogic/simulation/SimulationServer**')   
    exclude('Tiles/**')
    exclude('tiles.json')
    
    with jar
}


dependencies {
    testImplementation     'junit:junit:4.13'
    implementation 'org.json:json:20190722' 
}

